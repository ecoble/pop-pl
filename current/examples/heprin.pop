#lang pop-pl/current

require heparinPttCheckingV2
require infusion
//require ivInserted

initially 
   giveBolus 80 units/kg of: "heparin" by: "iv"
   start 18 units/kg/hour of: "heparin"

handler infusion is
  whenever new ptt
    whenever
    aPtt < 45        | giveBolus 80 units/kg of: "heparin" by: "iv"
                     | increase "heparin" by: 3 units/kg/hour

    45 < aPtt < 59   | giveBolus 40 units/kg of: "heparin" by: "iv"
                     | increase "heparin" by: 1 unit/kg/hour

    101 < aPtt < 123 | decrease "heparin" by: 1 unit/kg/hour

    aPtt > 123       | hold "heparin"
                     | after 1 hour
                     |     restart "heparin"
                     |     decrease "heparin" by: 3 units/kg/hour

// version 1
handler heparinPttCheckingV1 is
  Q 24 hours checkPtt
  whenever new change and drug is "heparin"
      after 6 hours
        checkPtt
  whenever new giveBolus and drug is "heparin"
      after 6 hours
        checkPtt


//version 2
handler heparinPttCheckingV2 is
  Q 6 hours checkPtt whenever not 59 < ptt < 101, 2 times
  Q 24 hours checkPtt whenever 59 < ptt < 101, 2 times


message ptt is [ aPtt ]
message change is [ drug by: value ]
message increase is change
message decrease(drug by: change) is change(drug by: -change)
message start is [ amount of: drug ]
message give is [ amount of: drug by: method ]
message giveBolus is give